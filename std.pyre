# Standard library utils

define STDOUT 1
define SYSCALL_WRITE 1

# Prints a string
macro print  # memory length
    swap STDOUT SYSCALL_WRITE syscall3
end

# Converts a decimal number into a character
# 0 to '0'
# 1 to '1'
# and so on
macro num2char  # num
    48 +
end

macro putchar  # char
    @ 1 swap STDOUT SYSCALL_WRITE syscall3 drop
end

macro show_uint  # num
    dup 0 = if  # If it's 0 just print 0
        dup num2char putchar
    else
        dup
        0 swap  # digits num

        while dup 0 > do
            dup 10 %  # last digit
            swap rot3
            1 +  # digits++
            swap
            10 / # num /= 10
        end drop

        # ... c b a digits
        while dup 0 > do
            swap num2char putchar
            1 -
        end drop
    end
    drop
    '\n' putchar
end

macro peek
    dup show_uint
end

macro get_uint8  # i
    memory swap + load  # memory[i]
end

macro set_uint8  # i -- value
    memory rot3 +  # value -- memory + i
    swap store  # memory[i] <- value
end

macro set_mem  # c a b
    while 2dup < do
        # b c a
        rot3 rot3 2dup swap set_uint8
        # c (a+1) b
        1 + rot3
    end
end

macro copy_mem  # dest source len
    # d s l
    2dup + swap drop
    # d s L s L
    while 2dup < do
        rot3 rot3           # L d s
        dup get_uint8       # L d s [s]
        rot3 dup rot3       # L s d d [s]
        set_uint8           # L s d
        1 + swap 1 + rot3   # (d+1) (s+1) L
    end drop drop drop
end

macro print_mem  # a b
    while 2dup < do
        swap # b a
        dup get_uint8 48 + putchar  # print 48 + [a]
        1 + swap
    end drop drop
    '\n' putchar
end
